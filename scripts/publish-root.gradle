
// Create variables with empty default values
ext["signing.keyId"] = ''
ext["signing.password"] = ''
ext["signing.key"] = ''
ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''
ext["sonatypeStagingProfileId"] = ''

// Set build variables
File secretPropsFile = project.rootProject.file('local.properties')
// Load properties from local.properties file if it exists
if (secretPropsFile.exists()) {
    println("TEMPO: publish-root.gradle (using props file)")
    Properties properties = new Properties()
    secretPropsFile.withInputStream { stream ->
        properties.load(stream)
    }
    properties.each { name, value ->
        ext[name] = value
        //println("--> ${name} | ${value} ")
    }
} else {
    println("TEMPO: publish-root.gradle (using sys env file)")
    ext["ossrhUsername"] = System.getenv('OSSRH_USERNAME')
    ext["ossrhPassword"] = System.getenv('OSSRH_PASSWORD')
    ext["sonatypeStagingProfileId"] = System.getenv('SONATYPE_STAGING_PROFILE_ID')
    ext["signing.keyId"] = System.getenv('SIGNING_KEY_ID')
    ext["signing.password"] = System.getenv('SIGNING_PASSWORD')
    ext["signing.key"] = System.getenv('SIGNING_KEY')
}

// Set up Sonatype Nexus Repository for Maven
if (ext["ossrhUsername"] && ext["ossrhPassword"]) {
    // Set up Sonatype repository
    nexusPublishing {
        repositories {
            sonatype {
                stagingProfileId = sonatypeStagingProfileId
                username = ossrhUsername
                password = ossrhPassword
                nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
                snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
                println("TEMPO: Sonatype Nexus repo set")
            }
        }
    }
} else {
    // Cannot set up Sonatype repository without credentials
    println("\nTEMPO: Build stopped - null or empty values found:" +
            "\nossrhUsername\t\t\t\t: ${ext["ossrhUsername"]}" +
            "\nossrhPassword\t\t\t\t: ${ext["ossrhPassword"]}")
}



